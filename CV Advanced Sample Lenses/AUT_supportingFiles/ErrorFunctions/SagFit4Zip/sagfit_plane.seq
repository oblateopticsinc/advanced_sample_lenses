!	Fit x,y,z data to a plane.
!
!	Argument #1 = name of tab-delimited text file containing x,y,z data, one data point per line
!
LCL STR ^filename
GBL NUM ^Amat(3,3) ^Xvec(3) ^Bvec(3)  !  ^Xvec will contain the coefficients.
^N == 3
wri q"Global arrays ^Amat('d','d'), ^Xvec('d'), and ^Bvec('d') created." ^N ^N ^N ^N
!
!	Step 1:  Read data into a buffer.  First find an empty buffer.
!
^b == 0
until
	^b == ^b + 1
end unt (buf.emp b^b)
^filename == rfstr(1)
buf imp b^b $filename
^npts == (buf.LST b^b)
!
!	Compute appropriate sums for LSQ fit
!
^sumx == 0
^sumy == 0
^sumx2 == 0
^sumxy == 0
^sumy2 == 0
^sumz == 0
^sumxz == 0
^sumyz == 0
for ^i 1 ^npts
	^x == (buf.num b^b i^i j1)
	^y == (buf.num b^b i^i j2)
	^z == (buf.num b^b i^i j3)
	^sumx == ^sumx + ^x
	^sumy == ^sumy + ^y
	^sumx2 == ^sumx2 + ^x*^x
	^sumxy == ^sumxy + ^x*^y
	^sumy2 == ^sumy2 + ^y*^y
	^sumz == ^sumz + ^z
	^sumxz == ^sumxz + ^x*^z
	^sumyz == ^sumyz + ^y*^z
end for
!
!	Put sums into A matrix & B vector
!
^Amat(1,1) == ^npts
^Amat(1,2) == ^sumx
^Amat(2,1) == ^sumx
^Amat(1,3) == ^sumy
^Amat(3,1) == ^sumy
^Amat(2,2) == ^sumx2
^Amat(2,3) == ^sumxy
^Amat(3,2) == ^sumxy
^Amat(3,3) == ^sumy2
wri ^Amat(1,1) ^Amat(1,2) ^Amat(1,3) 
wri ^Amat(2,1) ^Amat(2,2) ^Amat(2,3) 
wri ^Amat(3,1) ^Amat(3,2) ^Amat(3,3) 
^Bvec(1) == ^sumz
^Bvec(2) == ^sumxz
^Bvec(3) == ^sumyz
!
!	Call SVD macro to solve matrix
!
run solveNxNsvd 3
!
!	Write out the coefficients
!
for ^j 1 3
	wri concat(concat("Coef(",num_to_str(^j)),") =") ^Xvec(^j)
end for
!
!	Calculate RMS fit error
!
^rms == 0
for ^i 1 ^npts
	^x == (buf.num b^b i^i j1)
	^y == (buf.num b^b i^i j2)
	^z == (buf.num b^b i^i j3)
	^fiterror == ^z - (^Xvec(1) + ^Xvec(2)*^x + ^Xvec(3)*^y)
	wri ^i ^x ^y ^z ^fiterror
	^rms == ^rms + ^fiterror*^fiterror
end for
^rms == sqrtf(^rms/^npts)
wri "RMS fit error =" ^rms
wri q"Original X,Y,Z data are in buffer B'd'." ^b
