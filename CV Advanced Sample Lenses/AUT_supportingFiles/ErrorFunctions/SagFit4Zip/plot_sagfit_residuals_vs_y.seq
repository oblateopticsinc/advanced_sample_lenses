!	Macro for plotting sag fit residuals when S2 is decentered & tilted.
!	For use after SAGFIT2 and SAGFITAUT are run.
!	Utilizes global macro variables created by SAGFIT2.
!	Assumes ^NUMPTS of Y,Z data is in buffer B^sagbuf.
!	Fitted surface is surface 2.
!
!	Author:	J. Isenberg
!
!	History:	16-Jan-2003		First written
!
!	Arguments:	None (but assumes global variables ^NUMPTS & ^SAGBUF are defined)
!
!	Find an empty buffer after ^SAGBUF in
!	which to stash sag differences.
!
GBL NUM ^resbuf ^sagbuf ^numpts
LCL NUM ^rmsfiterror ^i ^x ^y ^bad ^diff ^n
for ^resbuf ^sagbuf 999
	if (buf.emp b^resbuf)
		goto got_buffer
	end if
end for
LBL got_buffer
!
!	Calculate differences & put in that buffer.
!	Also compute RMS fit error for display.
!
^rmsfiterror == 0
for ^i 1 ^NUMPTS
	^x == (buf.num b^sagbuf i^i j1)
	^y == (buf.num b^sagbuf i^i j2)
	^ok == raysin(0,0,^x,^y,0,0)
	^diff == (z s2 g1)-(buf.num b^sagbuf i^i j3)
	buf put b^resbuf i^i ^y ^diff
	^rmsfiterror == ^rmsfiterror + ^diff**2
end for
!
!	Plot the differences with UGR.
!
ugr;xla 'Y';yla 'Sag difference (fitted-actual)'
^title == concat('Sag fit residuals for data in   ',^filename)
^title == concat(^title,' fit to ')
^title == concat(^title,(typ sur s2))
title ^title
dtb b^resbuf ia ja PNT 'Sag difference'
go
!
^rmsfiterror == sqrtf(^rmsfiterror/^NUMPTS)
wri "RMS fit error =" ^rmsfiterror
wri "Includes decenters & tilts of S2 (if any)."
wri "You can also calculate the RMS fit error with"
wri "EVA sqrtf((AUT.ERF)/^NUMPTS)"
